#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout(local_size_x = 16, local_size_y  = 16) in;
layout (constant_id =  0) const uint ResolutionX = 1440;
layout (constant_id =  1) const uint ResolutionY = 900;
layout (constant_id =  2) const uint LightListMax = 4092;
// TileSize vector in sample code
layout (constant_id =  3) const uint TileWidthX = 64;
layout (constant_id =  4) const uint TileWidthY = 64;
// Grid dimension vector in sampler code.
layout (constant_id =  7) const uint TileCountZ = 256;
layout (constant_id =  8) const float NearPlane = 0.1f;
layout (constant_id =  9) const float FarPlane = 3000.0f;
layout (constant_id = 10) const float AmbientGlobal = 0.20f;

const uint TileCountX = (ResolutionX - 1) / TileWidthX + 1;
const uint TileCountY = (ResolutionY - 1) / TileWidthY + 1;

layout (set = 0, binding = 0) uniform _ubo {
    mat4 model;
    mat4 view;
    mat4 projectionClip;
    mat4 normal;
    vec4 viewPosition;
    uint numLights;
} UBO;

layout (set = 1, binding = 0, r8ui) uniform uimageBuffer Flags;
layout (set = 1, binding = 2, r32ui) uniform uimageBuffer gridLightCounts;
layout (set = 1, binding = 3, r32ui) uniform uimageBuffer gridLightCountTotal;
layout (set = 1, binding = 4, r32ui) uniform uimageBuffer gridLightCountOffsets;

uint CoordToIdx(uint i, uint j, uint k) {
    return TileCountX * TileCountY * k + TileCountX * j + i;
}

void main() {
    if (gl_GlobalInvocationID.z < TileCountZ && gl_GlobalInvocationID.y < TileCountY && gl_GlobalInvocationID.x < TileCountX) {
        uint idx = CoordToIdx(gl_GlobalInvocationID.z, gl_GlobalInvocationID.y, gl_GlobalInvocationID.x); 
        // Get quantity of lights active in this tile
        uint count = imageLoad(gridLightCounts, int(idx)).r;
        if (count > 0) {
            // Returned value is total light count up to this point
            uint offset = imageAtomicAdd(gridLightCountTotal, 0, count);
            if (offset < LightListMax) {
                // Still have room left for a light
                imageStore(gridLightCountOffsets, int(idx), uvec4(offset, 0, 0, 0));
            }
            else {
                // Out of room for lights.
                imageStore(gridLightCountOffsets, int(idx), uvec4(0));
            }
        }
    }
}

