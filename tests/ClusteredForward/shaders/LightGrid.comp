layout(local_size_x = 32) in;
SPC const uint ResolutionX = 1920;
SPC const uint ResolutionY = 1080;
SPC const uint TileCountX = (1920 - 1) / 64 + 1;
SPC const uint TileCountY = (1080 - 1) / 64 + 1;
SPC const uint TileCountZ = 256;
SPC const uint TileWidth = 64;
SPC const uint TileHeight = 64;
#pragma USE_RESOURCES Lights
#pragma USE_RESOURCES ClusteredForward

int CoordToIdx(uint i, uint j, uint k) {
    return int(TileCountX * TileCountY * k + TileCountX * j + i);
}

vec3 getViewSpacePos(vec3 pos_in) {
    return (UBO.view * vec4(pos_in,1.0f)).xyz;
}

vec2 viewPosToFragPos(vec3 view_pos) {
    vec4 clip_pos = UBO.projectionClip * vec4(view_pos,1.0f);
    vec3 ndc = clip_pos.xyz / clip_pos.w;
    return 0.50f * (1.0f + ndc.xy) *  vec2(float(ResolutionX),float(ResolutionY));
}

uvec3 viewPosToGrid(vec2 frag_pos, float view_z) {
    vec3 c;
    c.xy = frag_pos / vec2(float(TileWidth),float(TileHeight));
    c.z = min(float(TileCountZ - 1), max(0.f, float(TileCountZ) * log((-view_z - UBO.depth.x) / (UBO.depth.y - UBO.depth.x) + 1.0f)));
    return uvec3(c);
}

void markLightToSkip(uint light_idx, vec4 light_pos) {
    imageStore(positionRanges, int(light_idx), vec4(light_pos.xyz, 0.0f));
}

void main() {

    uint light_idx = gl_GlobalInvocationID.x;

    if (light_idx > UBO.numLights) {
        return;
    }

    vec4 pos_range_in = imageLoad(positionRanges, int(light_idx));

    vec3 vp = getViewSpacePos(pos_range_in.xyz);
    vec3 vp_min, vp_max;
    vp_min.x = vp.x - pos_range_in.w;
    vp_max.x = vp.x + pos_range_in.w;
    vp_min.yz = vp.yz + pos_range_in.w;
    vp_max.yz = vp.yz - pos_range_in.w;

    // restrict view z as it should be
    if ((vp_max.z >= -UBO.depth.x) || (vp_max.z <= -UBO.depth.y)) {
        markLightToSkip(light_idx, pos_range_in);
        return;
    }

    vp_min.z = min(-UBO.depth.x, vp_min.z);
    vp_max.z = max(-UBO.depth.y, vp_max.z);

    vec2 fp_a = viewPosToFragPos(vp_min);
    vec2 fp_b = viewPosToFragPos(vec3(vp_min.xy, vp_max.z));
    vec2 fp_c = viewPosToFragPos(vp_max);
    vec2 fp_d = viewPosToFragPos(vec3(vp_max.xy, vp_min.z));
    vec2 fp_min = min(fp_a, fp_b);
    vec2 fp_max = max(fp_c, fp_d);

    // restrict to frustum
    
    bool exit = false;
    exit = exit || (fp_min.x < 0.0f && fp_max.x < 0.0f) || (fp_min.y < 0.0f && fp_max.y < 0.0f);
    exit = exit || (fp_min.x >= float(ResolutionX) && fp_max.x >= float(ResolutionX)) || (fp_min.y >= float(ResolutionY) && fp_max.y >= float(ResolutionY));
    exit = exit || fp_min.x > fp_max.x || fp_min.y > fp_max.y;

    if (exit) {
        markLightToSkip(light_idx, pos_range_in);
        return;
    }
    

    fp_min.xy = max(vec2(0.0f), fp_min);
    fp_max.xy = min(vec2(float(ResolutionX),float(ResolutionY)) - 1.0f, fp_max);

    vec3 grid_min = viewPosToGrid(fp_min, vp_min.z);
    vec3 grid_max = viewPosToGrid(fp_max, vp_max.z);
    uvec3 bmin = uvec3(grid_min);
    uvec3 bmax = uvec3(grid_max);

    imageStore(lightBounds, int(light_idx * 6 + 0), uvec4(bmin.x, 0, 0, 0));
    imageStore(lightBounds, int(light_idx * 6 + 1), uvec4(bmin.y, 0, 0, 0));
    imageStore(lightBounds, int(light_idx * 6 + 2), uvec4(bmin.z, 0, 0, 0));
    imageStore(lightBounds, int(light_idx * 6 + 3), uvec4(bmax.x, 0, 0, 0));
    imageStore(lightBounds, int(light_idx * 6 + 4), uvec4(bmax.y, 0, 0, 0));
    imageStore(lightBounds, int(light_idx * 6 + 5), uvec4(bmax.z, 0, 0, 0));

    for (uint i = bmin.x; i <= bmax.x; ++i) {
        for (uint j = bmin.y; j <= bmax.y; ++j) {
            for (uint k = bmin.z; k <= bmax.z; ++k) {
                int idx = CoordToIdx(i, j, k);
                if (imageLoad(Flags, idx).r == 1) {
                    imageAtomicAdd(lightCounts, idx, 1);
                }
            }
        }
    }
}
